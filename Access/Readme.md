## Работа с Access

# Подготовка

Для работы с базами Access необходимо на сервере 1С (в клиент-серверном режиме) или на компьютере, где запускается клиентское приложение (файловая база, толстый клиент), установить один из следующих компонентов:
- [Полный пакет Microsoft Access](https://infostart.ru/redirect.php?url=aHR0cHM6Ly9wcm9kdWN0cy5vZmZpY2UuY29tL3J1LXJ1L2NvbXBhcmUtYWxsLW1pY3Jvc29mdC1vZmZpY2UtcHJvZHVjdHM/dGFiPTEmYW1wO09DSUQ9QUlENjc5NDcxX09PX0RMQ19RNHJlZnJlc2g=).
- [Microsoft Access Database Engine 2016 Redistributable](https://infostart.ru/redirect.php?url=aHR0cHM6Ly93d3cubWljcm9zb2Z0LmNvbS9lbi11cy9kb3dubG9hZC9kZXRhaWxzLmFzcHg/aWQ9NTQ5MjA=).
- [Microsoft Access 2016 Runtime](https://infostart.ru/redirect.php?url=aHR0cHM6Ly93d3cubWljcm9zb2Z0LmNvbS9ydS1SVS9kb3dubG9hZC9kZXRhaWxzLmFzcHg/aWQ9NTAwNDA=).

Установка Microsoft Access 2016 Runtime не требует покупки дополнительных лицензий, т.к. этот пакет содержит лишь среду выполнения, которая используется для запуска уже готовых решений. Средства разработки в ней отсутствуют. При этом в состав пакета также входит установщик ODBC-драйвера, который нам и нужен.

Подробнее про лицензировние можно [прочитать здесь](https://infostart.ru/redirect.php?url=aHR0cHM6Ly93d3cucmlwdGlkZWhvc3RpbmcuY29tL2Jsb2cvdGFnL21pY3Jvc29mdC1saWNlbnNpbmcv), в том числе и в контексте MS Access.

# Простые примеры

Чтение данных из базы Access из кода встроенного языка 1С:Предприятия можно выполнять следующим образом.
```bsl
ФайлБазы = "<путь к базе *.accdb/*.mdb>";

// Инициализация подключения к базе
СтрокаПодключения = "Driver={Microsoft Access Driver (*.mdb, *.accdb)};DBQ=" + ФайлБазы;
Connection = Новый COMОбъект("ADODB.Connection");	
Connection.Open(СтрокаПодключения);

// Формируем команды чтения данных из таблицы "Выгрузка_результата_компоновки"
Command = Новый COMОбъект("ADODB.Command");
Command.ActiveConnection = Connection;
Command.CommandText = "Select * FROM Выгрузка_результата_компоновки";
Command.CommandType = 1;
RecordSet = Новый COMОбъект("ADODB.RecordSet");
RecordSet = Command.Execute();

// Считываем все поля и выводим пользователю
ЗначенияСтрокой ="";
Пока RecordSet.EOF() = 0 Цикл
	
	Для НомерПоля = 0 по  Recordset.Fields.Count - 1 цикл
		ЗначенияСтрокой = ЗначенияСтрокой + " " + Recordset.Fields(НомерПоля).Value;
	КонецЦикла;
	
	Сообщить(ЗначенияСтрокой);		
	ЗначенияСтрокой = "";
	
	RecordSet.MoveNext();
	
КонецЦикла;

// Освобождаем ресурсы
RecordSet.Close();
Connection.Close();
```

Для выгрузки пример будет походим, но файл базы данных должен быть уже готовым к операции, в т.ч. и с необходимыми таблицами.
```bsl
Запрос = Новый Запрос;
Запрос.Текст = 
	"ВЫБРАТЬ
	|	&ТекущаяДата КАК Дата,
	|	""Привет из Access"" КАК Строка";	
Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата());
ТаблицаИсточник = Запрос.Выполнить().Выгрузить();

// Подготовленная база Access для выгрузки
ПутьКБазе = "C:\Access\ПростаяВыгрузка.accdb";

СтрокаПодключения = "Driver={Microsoft Access Driver (*.mdb, *.accdb)};DBQ=" + ПутьКБазе;
СоединениеКБазе = Новый COMОбъект("ADODB.Connection");	
СоединениеКБазе.Open(СтрокаПодключения);

ИмяТаблицы = "ПростаяВыгрузка";
Запись = Новый COMОбъект("ADODB.RecordSet");	
ТекстЗапроса = "SELECT * FROM " + ИмяТаблицы;	
Запись.Open(
	// Текст запроса 
	ТекстЗапроса, 
	// Соединение с базой
	СоединениеКБазе,
	// Указывает тип курсора, используемого в записей объекта.
	// CursorType (https://docs.microsoft.com/ru-ru/sql/ado/reference/ado-api/cursortypeenum?view=sql-server-2017)
	// 1 = adOpenKeyset. Использует курсор набора ключей. 
	1, 			  
	// Тип блокировки
	// LockTypeEnum (https://docs.microsoft.com/ru-ru/sql/ado/reference/ado-api/open-method-ado-recordset?view=sql-server-2017)
	// 3 = adLockOptimistic (Указывает, оптимистической блокировки, записей.)
	3
);

// Добавляем записи в таблицу базы Access
//	В исходном файле первая колонка содержит дату,
//	а во второй сохраняем строку.
Для Каждого СтрокаТаблицы Из ТаблицаИсточник Цикл
	
	Запись.AddNew();
	Запись.Fields(0).Value = СтрокаТаблицы.Дата;
	Запись.Fields(1).Value = СтрокаТаблицы.Строка;
	Запись.UpDate();
	
КонецЦикла;	

СоединениеКБазе.Close();
СоединениеКБазе = Неопределено;
```

# Подсистема работы с Access

Описание работы с Access в конфигурации "Помощник работы MS Office" этого репозитория.

- Выгрузка таблицы значений.
```bsl
Функция ВыгрузкаВТаблицуЗначенийНаСервере()
	
	// Подготавливаем таблицу значений для выгрузки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Значения_1.ПолеЧисло КАК ПолеЧисло,
		|	Значения_1.ПолеСтрока КАК ПолеСтрока,
		|	ИСТИНА КАК Булево,
		|	ДАТАВРЕМЯ(2018, 12, 31) КАК Дата
		|ПОМЕСТИТЬ Значения_1
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК ПолеЧисло,
		|		""Тест 1"" КАК ПолеСтрока
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2,
		|		""Тест 2""
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		3,
		|		""Тест 3"") КАК Значения_1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значения_2.ПолеЧисло КАК ПолеЧисло,
		|	Значения_2.ПолеСтрока КАК ПолеСтрока,
		|	ЛОЖЬ КАК Булево,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Дата
		|ПОМЕСТИТЬ Значения_2
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК ПолеЧисло,
		|		""Тест 1"" КАК ПолеСтрока
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2,
		|		""Тест 2""
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		3,
		|		""Тест 3"") КАК Значения_2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значения_1.ПолеСтрока КАК FieldTwo,
		|	Значения_2.ПолеСтрока КАК FieldFour,
		|	Значения_1.Булево КАК BoolT,
		|	Значения_2.Булево КАК BoolF,
		|	Значения_1.Дата КАК DateNormal,
		|	Значения_2.Дата КАК DateEmpty,
		|	ДАТАВРЕМЯ(1753, 1, 1) КАК DateTooSmall,
		|	ДАТАВРЕМЯ(100, 1, 1) КАК DateError,
		|	ДАТАВРЕМЯ(3999, 12, 31) КАК DateTooBig,
		|	ВЫРАЗИТЬ(1 КАК ЧИСЛО(5, 0)) КАК SimpleNumber,
		|	1923.55 + 65.04 КАК ComplexNumber,
		|	""You can actually use the Decimal data type in CREATE TABLE queries. However, this requires your statement to be executed either using ADO, or on a database that's been set to use ANSI-92 compatible syntax. To set your database to ANSI-92 compatible syntax: Go to File -> Options. Open the tab Object Designers. Go to Query Designer, and under SQL Server Compatible Syntax (ANSI 92), check This Database. Now you can just execute the query. Note that this affects all queries in the database, and affects queries in various ways. To execute a query using ADO:"" КАК LongString
		|ИЗ
		|	Значения_1 КАК Значения_1,
		|	Значения_2 КАК Значения_2";	
	ТаблицаВыгрузки = Запрос.Выполнить().Выгрузить();
	
	// Создаем каталог, куда будут выгружаться файлы баз данных
	КаталогВыгрузки = КаталогВременныхФайлов() + "ТестоваяВыгрузка";
	СоздатьКаталог(КаталогВыгрузки);
	
	// Выполняем непосредственно создание файла базы данных и выгрузку в него данных
	РезультатВыгрузки = 
		РаботаСAccessСервер.ВыгрузитьТаблицуЗначений(
			КаталогВыгрузки, 
			ТаблицаВыгрузки,
			,
			// Функцией "ПолучитьОграниченияВыгрузкиБазы" получаем параметры выгрузки.
			// В этом случае указываем, что макс. количество записей для одного файла базы данных равно 3.
			РаботаСAccessКлиентСервер.ПолучитьОграниченияВыгрузкиБазы(,3));
	
	// Формируем архив со всеми базами данных Access
	ИмяТаблицы = "ВыгрузкаТаблицыЗначений";
	Коллекциябаз = НайтиФайлы(КаталогВыгрузки, "*.accdb");
	ПутьКАрхиву = КаталогВыгрузки + "\" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd") + "_ВыгрузкаДанных_" + ИмяТаблицы + ".zip";
	Архив = Новый ЗаписьZipФайла(ПутьКАрхиву,, "Выгрузка данных в Access """ + ИмяТаблицы + """");
	Для Каждого ФайлБазы Из Коллекциябаз Цикл
		Архив.Добавить(ФайлБазы.ПолноеИмя, РежимСохраненияПутейZIP.НеСохранятьПути);
	КонецЦикла;
	Архив.Записать();		
	
	// Возвращаем двоичные данные архива
	Возврат Новый ДвоичныеДанные(ПутьКАрхиву); 
	
КонецФункции
```
- Выгрузка запроса.
```bsl
Функция ВыгрузитьЗапросНаСервере()
	
	// Подготавливаем запрос для выгрузки
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Значения_1.ПолеЧисло КАК ПолеЧисло,
		|	Значения_1.ПолеСтрока КАК ПолеСтрока,
		|	ИСТИНА КАК Булево,
		|	ДАТАВРЕМЯ(2018, 12, 31) КАК Дата
		|ПОМЕСТИТЬ Значения_1
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК ПолеЧисло,
		|		""Тест 1"" КАК ПолеСтрока
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2,
		|		""Тест 2""
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		3,
		|		""Тест 3"") КАК Значения_1
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значения_2.ПолеЧисло КАК ПолеЧисло,
		|	Значения_2.ПолеСтрока КАК ПолеСтрока,
		|	ЛОЖЬ КАК Булево,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Дата
		|ПОМЕСТИТЬ Значения_2
		|ИЗ
		|	(ВЫБРАТЬ
		|		1 КАК ПолеЧисло,
		|		""Тест 1"" КАК ПолеСтрока
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		2,
		|		""Тест 2""
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		3,
		|		""Тест 3"") КАК Значения_2
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Значения_1.ПолеСтрока КАК FieldTwo,
		|	Значения_2.ПолеСтрока КАК FieldFour,
		|	Значения_1.Булево КАК BoolT,
		|	Значения_2.Булево КАК BoolF,
		|	Значения_1.Дата КАК DateNormal,
		|	Значения_2.Дата КАК DateEmpty,
		|	ДАТАВРЕМЯ(1753, 1, 1) КАК DateTooSmall,
		|	ДАТАВРЕМЯ(100, 1, 1) КАК DateError,
		|	ДАТАВРЕМЯ(3999, 12, 31) КАК DateTooBig,
		|	ВЫРАЗИТЬ(1 КАК ЧИСЛО(5, 0)) КАК SimpleNumber,
		|	1923.55 + 65.04 КАК ComplexNumber,
		|	""You can actually use the Decimal data type in CREATE TABLE queries. However, this requires your statement to be executed either using ADO, or on a database that's been set to use ANSI-92 compatible syntax. To set your database to ANSI-92 compatible syntax: Go to File -> Options. Open the tab Object Designers. Go to Query Designer, and under SQL Server Compatible Syntax (ANSI 92), check This Database. Now you can just execute the query. Note that this affects all queries in the database, and affects queries in various ways. To execute a query using ADO:"" КАК LongString
		|ИЗ
		|	Значения_1 КАК Значения_1,
		|	Значения_2 КАК Значения_2";
	
	// Создаем каталог, куда будут выгружаться файлы баз данных
	КаталогВыгрузки = КаталогВременныхФайлов() + "ТестоваяВыгрузка";
	СоздатьКаталог(КаталогВыгрузки);
	
	// Выполняем непосредственно создание файла базы данных и выгрузку в него данных
	РезультатВыгрузки = 
		РаботаСAccessСервер.ВыгрузитьЗапрос(
			КаталогВыгрузки, 
			Запрос,
			,
			// Функцией "ПолучитьОграниченияВыгрузкиБазы" получаем параметры выгрузки.
			// В этом случае указываем, что макс. количество записей для одного файла базы данных равно 3.
			РаботаСAccessКлиентСервер.ПолучитьОграниченияВыгрузкиБазы(,3));
	
	// Формируем архив со всеми базами данных Access	
	ИмяТаблицы = "ВыгрузкаТаблицыЗначений";
	Коллекциябаз = НайтиФайлы(КаталогВыгрузки, "*.accdb");
	ПутьКАрхиву = КаталогВыгрузки + "\" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd") + "_ВыгрузкаДанных_" + ИмяТаблицы + ".zip";
	Архив = Новый ЗаписьZipФайла(ПутьКАрхиву,, "Выгрузка данных в Access """ + ИмяТаблицы + """");
	Для Каждого ФайлБазы Из Коллекциябаз Цикл
		Архив.Добавить(ФайлБазы.ПолноеИмя, РежимСохраненияПутейZIP.НеСохранятьПути);
	КонецЦикла;
	Архив.Записать();		
	
	// Возвращаем двоичные данные архива
	Возврат Новый ДвоичныеДанные(ПутьКАрхиву); 
	
КонецФункции
```
- Выгрузка результата компоновки данных. Может применяться для выгрузки результата отчетов на базе СКД, как это сделано в примере ниже.
```bsl
&НаСервере
Функция ВыгрузитьВAccessНаСервере()

	ОтчетНаСервере = РеквизитФормыВЗначение("Отчет");
	
	// Создаем каталог, куда будут выгружаться файлы баз данных
	КаталогВыгрузки = КаталогВременныхФайлов() + "ВыгрузкаИзСКДвAccess";
	СоздатьКаталог(КаталогВыгрузки);
	
	// Выполняем непосредственно создание файла базы данных и выгрузку в него данных
	РезультатВыгрузки = РаботаСAccessСервер.ВыгрузитьРезультатКомпоновки(
		КаталогВыгрузки,
		// Основная схема компоновки данных
		ОтчетНаСервере.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"),
		// Текущие настройки отчета
		Отчет.КомпоновщикНастроек.ПолучитьНастройки(),
		// Имя варианта отчета с заменой пробелов. Передается в качестве имени таблицы для выгрузки.
		СтрЗаменить(ЭтаФорма.ПредставлениеТекущегоВарианта, " ", "_"),
		// Функцией "ПолучитьОграниченияВыгрузкиБазы" получаем параметры выгрузки.
		// В этом случае указываем, что макс. количество записей для одного файла базы данных равно 2.
		РаботаСAccessКлиентСервер.ПолучитьОграниченияВыгрузкиБазы(,2,)
	);

	// Формируем архив со всеми базами данных Access
	ИмяТаблицы = "ВыгрузкаОтчетаСКД";
	Коллекциябаз = НайтиФайлы(КаталогВыгрузки, "*.accdb");
	ПутьКАрхиву = КаталогВыгрузки + "\" + Формат(ТекущаяДата(), "ДФ=yyyyMMdd") + "_ВыгрузкаДанных_" + ИмяТаблицы + ".zip";
	Архив = Новый ЗаписьZipФайла(ПутьКАрхиву,, "Выгрузка данных в Access """ + ИмяТаблицы + """");
	Для Каждого ФайлБазы Из Коллекциябаз Цикл
		Архив.Добавить(ФайлБазы.ПолноеИмя, РежимСохраненияПутейZIP.НеСохранятьПути);
	КонецЦикла;
	Архив.Записать();		
		
	// Возвращаем двоичные данные архива
	Возврат Новый ДвоичныеДанные(ПутьКАрхиву);

```


