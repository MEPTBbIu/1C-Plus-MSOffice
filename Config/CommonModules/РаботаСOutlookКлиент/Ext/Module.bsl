
#Область ПрограммныйИнтерфейс

Процедура СоздатьПисьмо(Тема, Содержимое, АдресПолучателя) Экспорт
	
	Outlook = ПриложениеOutlook();
	
	Письмо = Outlook.CreateItem(0);
	Письмо.Subject = Тема;
	Письмо.Body = Содержимое;
	Письмо.Recipients.Add(АдресПолучателя);
	
	Письмо.Display();	
	
КонецПроцедуры

Процедура СоздатьСобытие(Имя, Начало, Окончание, Содержимое) Экспорт
	
	Outlook = ПриложениеOutlook();
	
	Событие = Outlook.CreateItem(1);
	Событие.Subject = Имя;
	Событие.Start = Формат(Начало, "ДЛФ=DT");
	Событие.End = Формат(Окончание, "ДЛФ=DT");
	Событие.Body = Содержимое;
	
	Событие.Display();	
	
КонецПроцедуры

Процедура СоздатьЗадачу(Имя, Начало, Содержимое) Экспорт
	
	Outlook = ПриложениеOutlook();
	
	Задача = Outlook.CreateItem(3);
	Задача.Subject = Имя;
	Задача.StartDate = Формат(Начало, "ДЛФ=DT");
	Задача.Body = Содержимое;
	
	Задача.Display();
	
КонецПроцедуры

#КонецОбласти

#Область Служебный

Функция ПриложениеOutlook()
	
	Попытка
		
		Outlook = Новый COMОбъект("Outlook.Application");
		
	Исключение
		
		ВызватьИсключение 
			"Не удалось создать объект приложения ""Outlook""
			|
			|Ошибка: " + ОписаниеОшибки();
		
	КонецПопытки;
	
	Возврат Outlook;
	
КонецФункции

#КонецОбласти